func main()
	var file: file@Reader :: file@makeReader("input.txt")
	assert(file <>& null)
	
	const n: int :: 20
	var a: [][]int :: #[20, 20]int
	for i(0, n - 1)
		for j(0, n - 1)
			do a[i][j] :: file.readInt()
		end for
	end for
	
	var ans: int :: 0
	var dx: []int :: [1, 1, 0, 1]
	var dy: []int :: [0, 1, 1, -1]
	for sx(0, n - 1)
		for sy(0, n - 1)
			for dir(0, 3)
				var product: int :: 1
				var x: int :: sx
				var y: int :: sy
				for i(0, 3)
					if(x < 0 | n <= x | y < 0 | n <= y)
						do product :: 0
						break i
					end if
					do product :* a[y][x]
					do x :+ dx[dir]
					do y :+ dy[dir]
				end for
				do ans :: [ans, product].max()
			end for
		end for
	end for
	do cui@print("\{ans}\n")
end func
