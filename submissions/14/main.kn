var mp: dict<int, int>
var cnt: int :: 0
func main()
	do @mp :: #dict<int, int>
	do @mp.add(1, 1)
	func dfs(val: int): int
		var res: int :: @mp.get(val, &)
		if(res <> 0)
			ret res
		end if
		
		var v: int :: (val % 2 = 0) ?(val / 2, val * 3 + 1)
		do res :: @mp.get(v, &)
		if(res = 0)
			do res :: dfs(v)
		end if
		do res :+ 1
		do @mp.add(val, res)
		ret res
	end func
	
	var max: int :: 0
	var ans: int :: 0
	for i(2, 999999)
		var v: int :: dfs(i)
		if(v > max)
			do max :: v
			do ans :: i
		end if
	end for
	do cui@print("\{ans}\n")
end func
